apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    ndkVersion '22.0.6917172'
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    defaultConfig {
        applicationId "io.github.innoobwetrust.kintamanga"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode buildVersionCode()
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    signingConfigs {
        release {
            storeFile file("../signingKey.jks")
            storePassword = "$System.env.KEY_STORE_PWD"
            keyAlias = "$System.env.KEY_ALIAS"
            keyPassword = "$System.env.KEY_PWD"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    sourceSets {
        main.kotlin.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/java'
    }
}

kapt {
    correctErrorTypes = true
}

ext {
    // Dependency injection
    kodeinVersion = '4.1.0'
    glideVersion = '4.11.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.3.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Kotlin standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Dependency injection
    implementation "com.github.salomonbrys.kodein:kodein:$kodeinVersion"
    implementation "com.github.salomonbrys.kodein:kodein-conf:$kodeinVersion"

    // Network
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    // Persistent cookieJar
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // HTML parser
    implementation 'org.jsoup:jsoup:1.13.1'

    // Javascript engine
    implementation 'com.squareup.duktape:duktape-android:1.3.0'

    // Image loading library focused on smooth scrolling
    implementation "com.github.bumptech.glide:glide:4.11.0"
    implementation "com.github.bumptech.glide:annotations:4.11.0"
    kapt "com.github.bumptech.glide:compiler:4.11.0"
    implementation "com.github.bumptech.glide:okhttp3-integration:4.11.0@aar"
    implementation ("com.github.bumptech.glide:recyclerview-integration:4.11.0") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    // Image view
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'

    // ReactiveX
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'com.github.pwittchen:reactivenetwork:0.13.0'

    // DragSelectRecyclerView
    implementation 'com.afollestad:drag-select-recyclerview:1.0.0'
    implementation 'com.afollestad:material-cab:0.1.12'

    // Json
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'

    // Database
    implementation 'com.pushtorefresh.storio:sqlite:1.13.0'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        // Strange problem with AndroidX => https://stackoverflow.com/a/63423780
        if (requested.group == "androidx.appcompat") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "1.+"
            }
        }
    }
}
